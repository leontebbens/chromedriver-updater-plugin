plugins {
    id "com.gradle.plugin-publish" version "0.9.0"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()    
    compile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.2"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

jar {
    // include license into jar
    from 'LICENSE.txt'
}

// package source into a jar file
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// define artifacts for upload
artifacts {
    archives jar
    archives packageSources
}


def configurePom(def pom) {
    // ensure correct artifact ID
    pom.artifactId = 'chromedriver-updater-plugin'

    // pom file details
    pom.project {
        name 'chromedriver-updater-plugin'
        packaging 'jar'
        description 'Checks and downloads latest chromedriver for selenium'
        url 'https://github.com/leontebbens/chromedriver-updater-plugin'

        scm {
            url 'scm:git:git://github.com/leontebbens/chromedriver-updater-plugin.git'
            connection 'scm:git:git://github.com/leontebbens/chromedriver-updater-plugin.git'
            developerConnection 'scm:git:git://github.com/leontebbens/chromedriver-updater-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'leontebbens'
                name 'Leon Tebbens'
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        configurePom(pom)
    }
}


pluginBundle {
    website = 'https://github.com/leontebbens/chromedriver-updater-plugin'
    vcsUrl = 'https://github.com/leontebbens/chromedriver-updater-plugin.git'
    description = 'Auto-update Chromedriver for Selenium with this plugin'
    tags = ['gradle', 'selenium', 'plugin', 'chromedriver', 'jenkins']

    plugins {
        chromedriverUpdatePlugin {
            id = 'eu.leontebbens.gradle.chromedriver-updater'
            displayName = 'Chromedriver Updater plugin'
        }
    }
}


